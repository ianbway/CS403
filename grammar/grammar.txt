program : def | 
          def program

def : varDef | 
      funcDef

expression : unary | 
             unary operator expression

operator : PLUS | 
           MINUS | 
           MULTIPLY | 
           DIVIDE |
           LESS_THAN |
           LESS_THAN_EQUAL |
           EQUAL |
           GREATER_THAN |
           GREATER_THAN_EQUAL |
           MODULO

unary : INTEGER |
        REAL |
        VARIABLE |
        PLUS PLUS VARIABLE |
        MINUS unary |
        STRING |
        VARIABLE OPEN_BRACKET optArgList CLOSE_BRACKET |
        OPEN_BRACKET expression CLOSE_BRACKET |
        PRINT OPEN_BRACKET argList CLOSE_BRACKET

argList : expression |
          expression BAR argList

optArgList : argList |
             *empty*

paramList : VARIABLE |
            VARIABLE BAR paramList

optParamList : paramList |
               *empty*

if : IF OPEN_BRACKET expression CLOSE_BRACKET block elses

elses : ELSE block finally |
        ELSE if |
        *empty*

finally: block |
         *empty*

while : WHILE OPEN_BRACKET expression CLOSE_BRACKET block

block : OPEN_BRACKET OPEN_BRACKET statements CLOSE_BRACKET CLOSE_BRACKET |
        OPEN_BRACKET OPEN_BRACKET statements return CLOSE_BRACKET CLOSE_BRACKET

return : VARIABLE |
         NUMBER |
         MINUS NUMBER

statements : statement |
             statement statements

statement :  expression |
             if |
             while

varDef : VAR VARIABLE optInit

optInit : EQUAL expression |
          *empty*

funcDef : FUNCTION VARIABLE OPEN_BRACKET paramList CLOSE_BRACKET block