program : def | 
          def program |
          statement

def : varDef | 
      funcDef

expression : unary | 
             unary operator expression

operator : PLUS | 
           MINUS | 
           MULTIPLY | 
           DIVIDE |
           LESS_THAN |
           LESS_THAN_EQUAL |
           EQUAL |
           COMPARE_EQUAL |
           NOT EQUAL |
           GREATER_THAN |
           GREATER_THAN_EQUAL |
           MODULO |
           OR |
           AND

unary : INTEGER |
        REAL |
        varExpression |
        MINUS unary |
        NOT unary |
        STRING |
        OPEN_BRACKET expression CLOSE_BRACKET |
        AT OPEN_BRACKET optArgList CLOSE_BRACKET |
        PRINT OPEN_BRACKET argList CLOSE_BRACKET

varExpression : VARIABLE |
                VARIABLE OPEN_BRACKET optArgList CLOSE_BRACKET

argList : expression |
          expression BAR argList

optArgList : argList |
             *empty*

paramList : VARIABLE |
            VARIABLE BAR paramList

optParamList : paramList |
               *empty*

if : IF OPEN_BRACKET expression CLOSE_BRACKET block elses

elses : ELSE block |
        ELSE if |
        *empty*

while : WHILE OPEN_BRACKET expression CLOSE_BRACKET block

block : OPEN_BLOCK statements CLOSE_BLOCK

statements : statement |
             statement statements

statement :  expression |
             if |
             while |
             funcDef |
             varDef |
             RETURN expression

varDef : VAR VARIABLE optInit

optInit : EQUAL expression |
          *empty*

funcDef : FUNC VARIABLE OPEN_BRACKET optParamList CLOSE_BRACKET block